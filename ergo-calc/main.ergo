bring stdr
bring cogito

def display = cogito.textfield(@"0")
def ?current_value = 0.0
def ?stored_value = 0.0
def ?pending_op = @""
def ?reset_input = true
def ?has_error = false
def ABOUT_MORE_INFO_URL = @"https://github.com/lainsce/ergo"
def ABOUT_REPORT_BUG_URL = @"https://github.com/lainsce/ergo/issues"

fun update_display(text = string) (( -- )) {
    display.set_text(text)
}

fun clear_all() (( -- )) {
    current_value = 0.0
    stored_value = 0.0
    pending_op = @""
    reset_input = true
    has_error = false
    update_display(@"0")
}

fun commit_pending() (( -- )) {
    let op = pending_op
    if op == @"" {
        return
    }
    if reset_input {
        return
    }

    let lhs = stored_value
    let rhs = current_value

    if op == @"/" && rhs == 0 {
        has_error = true
        pending_op = @""
        reset_input = true
        update_display(@"Error")
        return
    }

    let ?result = 0.0
    if op == @"+" {
        result = lhs + rhs
    } elif op == @"-" {
        result = lhs - rhs
    } elif op == @"*" {
        result = lhs * rhs
    } else {
        result = lhs / rhs
    }

    current_value = result
    stored_value = result
    update_display(str(result))
}

fun input_digit(d = num) (( -- )) {
    if has_error {
        clear_all()
    }

    let ?next = current_value
    if reset_input {
        next = d
        reset_input = false
    } else {
        next = next * 10 + d
    }

    current_value = next
    update_display(str(next))
}

fun choose_operator(op = string) (( -- )) {
    if has_error {
        clear_all()
    }

    if pending_op != @"" && !reset_input {
        commit_pending()
        if has_error {
            return
        }
    } else {
        stored_value = current_value
    }

    pending_op = op
    reset_input = true
}

fun evaluate() (( -- )) {
    if has_error {
        clear_all()
        return
    }
    if pending_op == @"" {
        return
    }

    commit_pending()
    if has_error {
        return
    }

    pending_op = @""
    reset_input = true
}

fun digit_button(d = num) (( cogito.Button )) {
    let btn = cogito.button(str(d))
    btn.on_click((b = cogito.Button) => {
        input_digit(d)
    })
    return btn
}

fun operator_button(text = string, op = string) (( cogito.Button )) {
    let btn = cogito.button(text)
    cogito.set_class(btn, @"outlined")
    btn.on_click((b = cogito.Button) => {
        choose_operator(op)
    })
    return btn
}

fun clear_button() (( cogito.Button )) {
    let btn = cogito.button(@"C")
    cogito.set_class(btn, @"text")
    btn.on_click((b = cogito.Button) => {
        clear_all()
    })
    return btn
}

fun equals_button() (( cogito.Button )) {
    let btn = cogito.button(@"=")
    btn.on_click((b = cogito.Button) => {
        evaluate()
    })
    return btn
}

fun show_about_window(win = cogito.Window) (( -- )) {
    let dlg = cogito.dialog(@"")
    let root = cogito.vstack()
    root.set_gap(12)
    root.align_center()

    let icon = cogito.image(@"sf:equal")
    cogito.set_class(icon, @"about-window-icon")
    root.add(icon)

    let title = cogito.label(@"ErgoCalc")
    cogito.set_class(title, @"about-window-title")
    title.set_text_align(1)
    root.add(title)

    let license = cogito.label(@"MIT License")
    cogito.set_class(license, @"about-window-license")
    license.set_text_align(1)
    root.add(license)

    let links = cogito.hstack()
    links.set_gap(12)
    links.align_center()
    cogito.set_class(links, @"about-window-actions")

    let more_btn = cogito.button(@"More info")
    cogito.set_class(more_btn, @"outlined")
    more_btn.on_click((b = cogito.Button) => {
        cogito.open_url(ABOUT_MORE_INFO_URL)
    })
    links.add(more_btn)

    let bug_btn = cogito.button(@"Report a Bug")
    cogito.set_class(bug_btn, @"outlined")
    bug_btn.on_click((b = cogito.Button) => {
        cogito.open_url(ABOUT_REPORT_BUG_URL)
    })
    links.add(bug_btn)

    root.add(links)

    dlg.add(root)
    win.set_dialog(dlg)
}

fun build_ui(win = cogito.Window) (( -- )) {
    let root = cogito.vstack()
    root.set_gap(6)

    let bar = cogito.appbar(@"", @"")
    bar.set_hexpand(true)
    let about_btn = bar.add_button(@"sf:questionmark", (b = cogito.Button) => {
        show_about_window(win)
    })
    cogito.set_tooltip(about_btn, @"About ErgoCalc")
    root.add(bar)

    display.align_end()
    display.set_hexpand(true)
    display.set_editable(false)
    root.add(display)

    let keypad = cogito.grid(4)
    keypad.set_gap(12, 12)
    keypad.set_hexpand(true)

    keypad.add(digit_button(7))
    keypad.add(digit_button(8))
    keypad.add(digit_button(9))
    keypad.add(operator_button(@"/", @"/"))

    keypad.add(digit_button(4))
    keypad.add(digit_button(5))
    keypad.add(digit_button(6))
    keypad.add(operator_button(@"*", @"*"))

    keypad.add(digit_button(1))
    keypad.add(digit_button(2))
    keypad.add(digit_button(3))
    keypad.add(operator_button(@"-", @"-"))

    keypad.add(digit_button(0))
    keypad.add(clear_button())
    keypad.add(equals_button())
    keypad.add(operator_button(@"+", @"+"))

    root.add(keypad)
    win.add(root)

    clear_all()
}

entry () (( -- )) {
    let app = cogito.app()
    app.set_accent_color(@"#8C56BF", false)
    let win = cogito.window_size(@"Ergo Calc", 300, 420)
    win.build(build_ui)
    win.set_resizable(false)
    app.set_appid(@"ergo.cogito.Calc")
    app.set_app_name(@"ErgoCalc")
    app.run(win)
}
