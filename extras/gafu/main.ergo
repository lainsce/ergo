    cask main

    bring stdr
    bring cogito

    def gafu_app = cogito.app()
    def source_picker = cogito.colorpicker()
    def upload_hint = cogito.label(@"Choose File Or Drag & Drop Here…")
    def scheme_switch = cogito.switch(@"")
    def ?source_hex = @"#72dec2"
    def ?selected_image_path = @""
    def selected_image_preview = cogito.image(@"")
    def ENSOR_DEFAULT = 0
    def ENSOR_CONTENT = 5
    def mode_moon = cogito.iconbtn(@"sf:moon")
    def mode_base = cogito.iconbtn(@"sf:sun.max")
    def mode_bright = cogito.iconbtn(@"sf:sun.lefthalf.filled")
    def mode_brightest = cogito.iconbtn(@"sf:sun.max.fill")
    def dark_switch = cogito.switchbar(@"Dark Mode")

    fun apply_source_color(hex = string) (( -- )) {
        if len(hex) == 0 {
            return
        }
        source_hex = hex
        source_picker.set_hex(source_hex)
        gafu_app.set_accent_color(source_hex, false)
    }

    fun choose_source_image() (( -- )) {
        let picked = stdr.open_file_dialog(@"Choose image", @"png")
        if is_null(picked) {
            return
        }

        let path = str(picked)
        if len(path) == 0 {
            return
        }

        selected_image_path = path
        scheme_switch.set_disabled(false)
        upload_hint.set_text(@"Image Selected.")
        selected_image_preview.set_source(selected_image_path)
        selected_image_preview.set_size(360, 200)
        selected_image_preview.set_radius(26)
        let seeded = gafu_app.set_accent_from_image(selected_image_path, false)
        if seeded != @"" {
            apply_source_color(seeded)
        }
    }

    fun reset_everything() (( -- )) {
        selected_image_path = @""
        upload_hint.set_text(@"Choose File Or Drag & Drop Here…")
        selected_image_preview.set_source(@"")
        selected_image_preview.set_size(0, 0)
        scheme_switch.set_checked(false)
        scheme_switch.set_disabled(true)

        gafu_app.set_ensor_variant(ENSOR_DEFAULT)
        apply_source_color(@"#72dec2")

        set_mode_selection(1, mode_moon, mode_base, mode_bright, mode_brightest)
        gafu_app.set_contrast(0)

        dark_switch.set_checked(false)
        gafu_app.set_dark_mode(false, false)
    }

    fun swatch(text = string, cls = string) (( cogito.Button )) {
        let box = cogito.button(text)
        .set_class(cls)
        .set_hexpand(true)
        return box
    }

    fun tone_column(a = string, ca = string, b = string, cb = string, c = string, cc = string, d = string, cd = string) (( cogito.VStack )) {
        let col = cogito.vstack()
        .set_hexpand(true)
        .set_class(@"gafu-tone-col")

        col.add(swatch(a, ca))
        col.add(swatch(b, cb))
        col.add(swatch(c, cc))
        col.add(swatch(d, cd))
        return col
    }

    fun fixed_column(a = string, ca = string, b = string, cb = string, c = string, cc = string, d = string, cd = string) (( cogito.VStack )) {
        let col = cogito.vstack()
        .set_hexpand(true)

        let top = cogito.hstack()
        .set_hexpand(true)
        top.add(swatch(a, ca))
        top.add(swatch(b, cb))

        col.add(top)
        col.add(swatch(c, cc))
        col.add(swatch(d, cd))
        return col
    }

    fun set_mode_selection(
        active = num,
        moon = cogito.IconButton,
        base = cogito.IconButton,
        bright = cogito.IconButton,
        brightest = cogito.IconButton
    ) (( -- )) {
        moon.set_checked(active == 0)
        base.set_checked(active == 1)
        bright.set_checked(active == 2)
        brightest.set_checked(active == 3)

        moon.set_class(if active == 0 { @"filled" } else { @"tonal" })
        base.set_class(if active == 1 { @"filled" } else { @"tonal" })
        bright.set_class(if active == 2 { @"filled" } else { @"tonal" })
        brightest.set_class(if active == 3 { @"filled" } else { @"tonal" })
    }

    fun build_palette_grid() (( cogito.VStack )) {
        let group = cogito.vstack()
        .set_hexpand(true)
        .set_padding(12, 18, 18, 18)

        let tones = cogito.hstack()
        .set_gap(12)
        .set_hexpand(true)

        tones.add(tone_column(@"Primary", @"primary", @"On Primary", @"on-primary", @"Primary Container", @"primary-container", @"On Primary Container", @"on-primary-container"))
        tones.add(tone_column(@"Secondary", @"secondary", @"On Secondary", @"on-secondary", @"Secondary Container", @"secondary-container", @"On Secondary Container", @"on-secondary-container"))
        tones.add(tone_column(@"Tertiary", @"tertiary", @"On Tertiary", @"on-tertiary", @"Tertiary Container", @"tertiary-container", @"On Tertiary Container", @"on-tertiary-container"))
        tones.add(tone_column(@"Error", @"error", @"On Error", @"on-error", @"Error Container", @"error-container", @"On Error Container", @"on-error-container"))

        let fixed = cogito.hstack()
        .set_gap(12)
        .set_hexpand(true)

        fixed.add(fixed_column(@"Primary Fixed", @"primary-fixed", @"Primary Fixed Dim", @"primary-fixed-dim", @"On Primary Fixed", @"on-primary-fixed", @"On Primary Fixed Variant", @"on-primary-fixed-variant"))
        fixed.add(fixed_column(@"Secondary Fixed", @"secondary-fixed", @"Secondary Fixed Dim", @"secondary-fixed-dim", @"On Secondary Fixed", @"on-secondary-fixed", @"On Secondary Fixed Variant", @"on-secondary-fixed-variant"))
        fixed.add(fixed_column(@"Tertiary Fixed", @"tertiary-fixed", @"Tertiary Fixed Dim", @"tertiary-fixed-dim", @"On Tertiary Fixed", @"on-tertiary-fixed", @"On Tertiary Fixed Variant", @"on-tertiary-fixed-variant"))

        let lower = cogito.hstack()
        .set_gap(12)
        .set_hexpand(true)

        let neutral = cogito.vstack()
        .set_gap(12)
        .set_hexpand(true)

        let surfaces = cogito.hstack()
        .set_hexpand(true)
        surfaces.add(swatch(@"Surface Dim", @"surface-dim"))
        surfaces.add(swatch(@"Surface", @"surface"))
        surfaces.add(swatch(@"Surface Bright", @"surface-bright"))

        let containers = cogito.hstack()
        .set_hexpand(true)
        containers.add(swatch(@"Surface Container Lowest", @"surface-container-lowest"))
        containers.add(swatch(@"Surface Container Low", @"surface-container-low"))
        containers.add(swatch(@"Surface Container", @"surface-container"))
        containers.add(swatch(@"Surface Container High", @"surface-container-high"))
        containers.add(swatch(@"Surface Container Highest", @"surface-container-highest"))

        let on_surface = cogito.hstack()
        .set_hexpand(true)
        on_surface.add(swatch(@"On Surface", @"on-surface"))
        on_surface.add(swatch(@"On Surface Variant", @"on-surface-variant"))
        on_surface.add(swatch(@"Outline", @"outline-fill"))
        on_surface.add(swatch(@"Border", @"outline-variant-fill"))

        neutral.add(surfaces)
        neutral.add(containers)
        neutral.add(on_surface)

        let inverse = cogito.vstack()
        .set_gap(12)
        .set_hexpand(true)

        let shadow_row = cogito.hstack()
        .set_hexpand(true)
        shadow_row.add(swatch(@"Shadow", @"shadow"))
        shadow_row.add(swatch(@"Scrim", @"scrim"))

        inverse.add(shadow_row)
        inverse.add(swatch(@"Inverse Surface", @"inverse-surface"))
        inverse.add(swatch(@"On Inverse Surface", @"on-inverse-surface"))
        inverse.add(swatch(@"Inverse Primary", @"inverse-primary-fill"))

        lower.add(neutral)
        lower.add(inverse)

        group.add(tones)
        group.add(fixed)
        group.add(lower)
        return group
    }

    fun build_right_panel() (( cogito.VStack )) {
        let panel = cogito.vstack()
        .set_gap(24)
        .set_padding(0, 18, 18, 18)
        .set_vexpand(true)
        .set_hexpand(true)
        .set_class(@"surface-container")

        let upload = cogito.zstack()
        .halign(1)
        .valign(1)
        .set_class(@"gafu-upload-wrap")

        let upload_hit = cogito.button(@"")
        .set_class(@"secondary-container")
        .set_class(@"gafu-upload-hit")
        .set_hexpand(true)
        .set_vexpand(true)
        .on_click((b = cogito.Button) => {
            choose_source_image()
        })
        upload.add(upload_hit)

        let upload_content = cogito.vstack()
        .set_class(@"gafu-upload-content")

        let image = cogito.image(@"sf:photo.badge.plus").set_class(@"gafu-upload-icon")
        upload_content.add(image)
        upload_hint.set_class(@"gafu-upload-label")
        upload_content.add(upload_hint)
        upload.add(upload_content)

        selected_image_preview
        .set_class(@"gafu-selected-image")
        .set_size(0, 0)
        .set_hexpand(true)
        upload.add(selected_image_preview)

        let tools = cogito.hstack()
        .set_gap(10)
        .set_class(@"gafu-tools")
        .halign(1)
        tools.add(cogito.image(@"sf:laptopcomputer").set_class(@"gafu-tool"))
        tools.add(scheme_switch
        .set_checked(false)
        .set_class(@"gafu-tool-switch")
        .set_disabled(len(selected_image_path) == 0)
        .on_change((sw = cogito.Switch) => {
            if sw.checked() {
                gafu_app.set_ensor_variant(ENSOR_CONTENT)
            } else {
                gafu_app.set_ensor_variant(ENSOR_DEFAULT)
            }
        }))
        tools.add(cogito.image(@"sf:photo").set_class(@"gafu-tool"))

        let modes = cogito.buttongroup()
        .set_class(@"gafu-modes")
        .set_class(@"surface-container-lowest")
        .set_halign(1)

        mode_moon
        .set_toggle(true)
        .set_checked(false)
        .set_class(@"tonal")

        mode_base
        .set_toggle(true)
        .set_checked(true)
        .set_class(@"filled")

        mode_bright
        .set_toggle(true)
        .set_checked(false)
        .set_class(@"tonal")

        mode_brightest
        .set_toggle(true)
        .set_checked(false)
        .set_class(@"tonal")

        mode_moon.on_click((b = cogito.IconButton) => {
            set_mode_selection(0, mode_moon, mode_base, mode_bright, mode_brightest)
            gafu_app.set_contrast(-1)
        })

        mode_base.on_click((b = cogito.IconButton) => {
            set_mode_selection(1, mode_moon, mode_base, mode_bright, mode_brightest)
            gafu_app.set_contrast(0)
        })

        mode_bright.on_click((b = cogito.IconButton) => {
            set_mode_selection(2, mode_moon, mode_base, mode_bright, mode_brightest)
            gafu_app.set_contrast(0.5)
        })

        mode_brightest.on_click((b = cogito.IconButton) => {
            set_mode_selection(3, mode_moon, mode_base, mode_bright, mode_brightest)
            gafu_app.set_contrast(1)
        })

        modes.add(mode_moon)
        modes.add(mode_base)
        modes.add(mode_bright)
        modes.add(mode_brightest)

        let source = cogito.card(@"")
        .set_gap(10)
        .set_hexpand(true)
        .set_class(@"surface-container-high")
        .set_class(@"gafu-source")
        source.add(
            cogito.label(@"Source Color")
            .valign(1)
        )

        let source_controls = cogito.hstack()
        .set_gap(6)

        source_picker.set_class(@"gafu-source-picker")
        .set_hex(source_hex)
        .set_hexpand(true)
        .on_change((cp = cogito.ColorPicker) => {
            apply_source_color(cp.hex())
        })
        source_controls.add(source_picker)

        source_controls.add(cogito.iconbtn(@"sf:doc.on.doc").set_class(@"iconic").on_click((b = cogito.Button) => {
            gafu_app.copy_to_clipboard(source_hex)
        }))

        source.add(source_controls)

        dark_switch
        .set_checked(false)
        .set_class(@"surface-container-high")
        .on_change((sw = cogito.SwitchBar) => {
            gafu_app.set_dark_mode(sw.get_checked(), false)
        })

        panel.add(upload)
        panel.add(tools)
        panel.add(modes)
        panel.add(source)
        panel.add(dark_switch)
        return panel
    }

    entry() (( -- )) {
        let app = gafu_app
        .set_appid(@"ergo.cogito.Gafu")
        .set_app_name(@"Gafu")
        .set_ensor_variant(ENSOR_DEFAULT)
        .set_dark_mode(false, false)
        .set_accent_color(@"#72dec2", false)

        cogito.load_sum(@"gafu.sum")

        let win = cogito.window_size(@"Gafu", 1720, 760)
        .set_autosize(false)
        .set_resizable(true)

        let root = cogito.hstack()
        .set_hexpand(true)
        .set_vexpand(true)

        let left = cogito.vstack()
        .set_hexpand(true)
        .add(cogito.appbar(@"Palette", @"").set_class(@"gafu-left-bar"))
        .add(build_palette_grid())

        root.add(left)

        let right = cogito.vstack()
        .set_vexpand(true)

        let bar = cogito.appbar(@"Source Color", @"")
        .set_class(@"surface-container")
        bar.add_button(@"sf:arrow.clockwise", (b = cogito.Button) => {
            reset_everything()
        })

        right.add(bar)
        right.add(build_right_panel())
        root.add(right)

        win.add(root)
        .set_resizable(true)
        app.run(win)
    }
