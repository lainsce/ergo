cask gui_gallery

bring stdr
bring cogito

fun build_buttons_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Button variants")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let status = cogito.label(@"Try each button.")
    status.set_class(@"body")
    status.set_wrap(true)
    vstack.add(status)

    let btn1 = cogito.button(@"Filled Button")
    btn1.on_click((b = cogito.Button) => {
        b.set_text(@"Clicked!")
        status.set_text(@"Filled button clicked.")
    })
    vstack.add(btn1)

    let btn2 = cogito.button(@"Outlined Button")
    cogito.set_class(btn2, @"outlined")
    btn2.on_click((b = cogito.Button) => {
        status.set_text(@"Outlined button activated.")
    })
    vstack.add(btn2)

    let btn3 = cogito.button(@"Text Button")
    cogito.set_class(btn3, @"text")
    btn3.on_click((b = cogito.Button) => {
        status.set_text(@"Text button pressed.")
    })
    vstack.add(btn3)

    let iconbtn = cogito.iconbtn(@"star")
    cogito.set_tooltip(iconbtn, @"Icon button")
    iconbtn.on_click((b = cogito.Button) => {
        status.set_text(@"Icon button pressed.")
    })
    vstack.add(iconbtn)

    return vstack
}

fun build_textfields_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Text input fields")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let preview = cogito.label(@"Type in any field to update this preview.")
    preview.set_class(@"body")
    preview.set_wrap(true)
    vstack.add(preview)

    let tf = cogito.textfield(@"Text field")
    tf.set_hexpand(true)
    tf.on_change((field = cogito.TextField) => {
        let text = field.text()
        preview.set_text(@"TextField: $text")
    })
    vstack.add(tf)

    let sf = cogito.searchfield(@"Search…")
    sf.set_hexpand(true)
    sf.on_change((field = cogito.SearchField) => {
        let text = field.text()
        preview.set_text(@"SearchField: $text")
    })
    vstack.add(sf)

    let tv = cogito.textview(@"Multi-line text view")
    tv.set_hexpand(true)
    tv.on_change((field = cogito.TextView) => {
        preview.set_text(@"TextView changed.")
    })
    vstack.add(tv)

    let clear_btn = cogito.button(@"Clear Fields")
    clear_btn.on_click((b = cogito.Button) => {
        tf.set_text(@"")
        sf.set_text(@"")
        tv.set_text(@"")
        preview.set_text(@"All text fields cleared.")
    })
    vstack.add(clear_btn)

    return vstack
}

fun build_checkboxes_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Selection controls")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let status = cogito.label(@"Toggle controls to see state changes.")
    status.set_class(@"body")
    status.set_wrap(true)
    vstack.add(status)

    let cb1 = cogito.checkbox(@"Checkbox 1", null)
    cb1.on_change((c = cogito.Checkbox) => {
        let checked = c.checked()
        status.set_text(@"Checkbox 1: $checked")
    })
    vstack.add(cb1)

    let cb2 = cogito.checkbox(@"Checkbox 2", null)
    cb2.on_change((c = cogito.Checkbox) => {
        let checked = c.checked()
        status.set_text(@"Checkbox 2: $checked")
    })
    vstack.add(cb2)

    let r1 = cogito.checkbox(@"Radio A", @"group1")
    r1.on_change((c = cogito.Checkbox) => {
        if c.checked() {
            status.set_text(@"Radio selected: A")
        }
    })
    vstack.add(r1)

    let r2 = cogito.checkbox(@"Radio B", @"group1")
    r2.on_change((c = cogito.Checkbox) => {
        if c.checked() {
            status.set_text(@"Radio selected: B")
        }
    })
    vstack.add(r2)

    let sw = cogito.switch(@"Toggle Switch")
    sw.on_change((s = cogito.Switch) => {
        let checked = s.checked()
        status.set_text(@"Switch: $checked")
    })
    vstack.add(sw)

    let set_btn = cogito.button(@"Set Defaults")
    set_btn.on_click((b = cogito.Button) => {
        cb1.set_checked(true)
        cb2.set_checked(false)
        r1.set_checked(true)
        sw.set_checked(true)
        status.set_text(@"Defaults applied.")
    })
    vstack.add(set_btn)

    return vstack
}

fun build_sliders_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Slider & Stepper")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let value_label = cogito.label(@"Slider: 50")
    value_label.set_class(@"body")
    vstack.add(value_label)

    let sld = cogito.slider(0, 100, 50)
    sld.set_hexpand(true)
    sld.on_change((s = cogito.Slider) => {
        let value = s.value()
        value_label.set_text(@"Slider: $value")
    })
    vstack.add(sld)

    let step = cogito.stepper(0, 100, 5, 1)
    vstack.add(step)

    let reset_btn = cogito.button(@"Reset Slider")
    reset_btn.on_click((b = cogito.Button) => {
        sld.set_value(50)
        value_label.set_text(@"Slider: 50")
    })
    vstack.add(reset_btn)

    return vstack
}

fun build_pickers_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Date & Color pickers")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let status = cogito.label(@"Pick a date or color.")
    status.set_class(@"body")
    status.set_wrap(true)
    vstack.add(status)

    let dp = cogito.datepicker()
    dp.on_change((d = cogito.DatePicker) => {
        status.set_text(@"Date picker changed.")
    })
    vstack.add(dp)

    let cp = cogito.colorpicker()
    cp.on_change((c = cogito.ColorPicker) => {
        status.set_text(@"Color picker changed.")
    })
    vstack.add(cp)

    return vstack
}

fun build_lists_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Lists & Grids")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let list_status = cogito.label(@"Select a list row.")
    list_status.set_class(@"body")
    vstack.add(list_status)

    let list = cogito.list()
    list.set_hexpand(true)
    list.add(cogito.label(@"List Item 1"))
    list.add(cogito.label(@"List Item 2"))
    list.add(cogito.label(@"List Item 3"))
    list.on_select((idx) => {
        list_status.set_text(@"List selected index: $idx")
    })
    list.on_activate((idx) => {
        list_status.set_text(@"List activated index: $idx")
    })
    vstack.add(list)

    let grid_status = cogito.label(@"Select a grid cell.")
    grid_status.set_class(@"body")
    vstack.add(grid_status)

    let grid = cogito.grid(2)
    grid.set_hexpand(true)
    grid.set_gap(8, 8)
    grid.add(cogito.label(@"A1"))
    grid.add(cogito.label(@"A2"))
    grid.add(cogito.label(@"B1"))
    grid.add(cogito.label(@"B2"))
    grid.on_select((idx) => {
        grid_status.set_text(@"Grid selected index: $idx")
    })
    vstack.add(grid)

    return vstack
}

fun build_controls_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Dropdown & Progress")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let status = cogito.label(@"Select a severity level.")
    status.set_class(@"body")
    vstack.add(status)

    let dd = cogito.dropdown()
    dd.set_items([@"Low", @"Medium", @"High", @"Critical"])
    dd.set_selected(1)
    dd.set_hexpand(true)
    vstack.add(dd)

    let prog_label_container = cogito.hstack()
    let prog_label = cogito.label(@"Progress (65%)")
    prog_label_container.add(prog_label)
    vstack.add(prog_label_container)

    let prog = cogito.progress(0.65)
    prog.set_hexpand(true)
    vstack.add(prog)

    dd.on_change((idx) => {
        let next = if idx == 0 {
            (0.25, @"Progress (25%)", @"Low severity selected.")
        } elif idx == 1 {
            (0.50, @"Progress (50%)", @"Medium severity selected.")
        } elif idx == 2 {
            (0.75, @"Progress (75%)", @"High severity selected.")
        } else {
            (1.0, @"Progress (100%)", @"Critical severity selected.")
        }
        prog.set_value(next[0])
        prog_label.set_text(next[1])
        status.set_text(next[2])
    })

    let row = cogito.hstack()
    row.set_gap(8)
    let inc_btn = cogito.button(@"Increase")
    inc_btn.on_click((b = cogito.Button) => {
        let proposed = prog.value() + 0.1
        if proposed > 1.0 {
            prog.set_value(1.0)
            prog_label.set_text(@"Progress (100%)")
        } else {
            prog.set_value(proposed)
            prog_label.set_text(@"Progress increased")
        }
    })
    row.add(inc_btn)
    let reset_btn = cogito.button(@"Reset")
    reset_btn.on_click((b = cogito.Button) => {
        dd.set_selected(1)
        prog.set_value(0.5)
        prog_label.set_text(@"Progress (50%)")
        status.set_text(@"Reset to Medium.")
    })
    row.add(reset_btn)
    vstack.add(row)

    let img_label_container = cogito.hstack()
    let img_label = cogito.label(@"Image (with corner radius)")
    img_label_container.add(img_label)
    vstack.add(img_label_container)

    let img = cogito.image(@"")
    img.set_source(@"cogito/examples/assets/image.jpg")
    img.set_size(360, 360)
    img.set_radius(12)
    vstack.add(img)

    return vstack
}

fun build_tabs_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Tabs & Segmented")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let status = cogito.label(@"Select a tab to switch content.")
    status.set_class(@"body")
    vstack.add(status)

    let tabs = cogito.tabs()
    tabs.set_items([@"Overview", @"Details", @"History", @"Miscellania"])
    tabs.set_ids([@"tab_overview", @"tab_details", @"tab_history", @"tab_misc"])
    tabs.set_selected(0)
    tabs.on_change((idx) => {
        status.set_text(@"Tab selected index: $idx")
    })
    tabs.set_hexpand(true)
    vstack.add(tabs)

    let tab_views = cogito.view_switcher()
    tab_views.set_hexpand(true)
    tab_views.add(@"tab_overview", (vs = cogito.ViewSwitcher) => cogito.label(@"Overview content"))
    tab_views.add(@"tab_details", (vs = cogito.ViewSwitcher) => cogito.label(@"Details content"))
    tab_views.add(@"tab_history", (vs = cogito.ViewSwitcher) => cogito.label(@"History content"))
    tab_views.add(@"tab_misc", (vs = cogito.ViewSwitcher) => cogito.label(@"Misc. content"))
    tab_views.set_active(@"tab_overview")
    tabs.bind(tab_views)
    vstack.add(tab_views)

    let goto_history = cogito.button(@"Go To History Tab")
    goto_history.on_click((b = cogito.Button) => {
        tabs.set_selected(2)
        tab_views.set_active(@"tab_history")
        status.set_text(@"Switched to History tab.")
    })
    vstack.add(goto_history)

    let seg = cogito.segmented()
    seg.add(cogito.label(@"Day"))
    seg.add(cogito.label(@"Week"))
    seg.add(cogito.label(@"Month"))
    seg.add(cogito.label(@"Year"))
    vstack.add(seg)

    return vstack
}

fun build_chips_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Chips & FAB")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let status = cogito.label(@"Interact with chips or the FAB.")
    status.set_class(@"body")
    vstack.add(status)

    let row = cogito.hstack()
    row.set_gap(8)

    let c1 = cogito.chip(@"Filter")
    c1.on_click((c = cogito.Chip) => {
        status.set_text(@"Filter chip clicked.")
    })
    row.add(c1)

    let c2 = cogito.chip(@"Selected")
    c2.set_selected(true)
    c2.on_click((c = cogito.Chip) => {
        let selected = c.selected()
        status.set_text(@"Selected chip toggled: $selected")
    })
    row.add(c2)

    let c3 = cogito.chip(@"Closable")
    c3.set_closable(true)
    c3.on_close((c = cogito.Chip) => {
        status.set_text(@"Closable chip close action triggered.")
    })
    row.add(c3)

    vstack.add(row)

    let fab_label_container = cogito.hstack()
    let fab_label = cogito.label(@"Floating Action Button")
    fab_label_container.add(fab_label)
    vstack.add(fab_label_container)

    let fab = cogito.fab(@"plus")
    fab.on_click((f = cogito.FAB) => {
        status.set_text(@"FAB clicked.")
    })
    vstack.add(fab)

    return vstack
}

fun build_carousel_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Carousel")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let status = cogito.label(@"Drag the carousel to scroll. Active item: 0")
    status.set_class(@"body")
    status.set_wrap(true)
    vstack.add(status)

    let carousel = cogito.carousel()
    carousel.set_hexpand(true)

    let item1 = cogito.carousel_item()
    item1.set_vexpand(true)
    item1.set_hexpand(true)
    item1.set_text(@"Mountain View")
    item1.set_halign(0)  -- left
    item1.set_valign(2)  -- bottom
    let img1 = cogito.image(@"")
    img1.set_source(@"cogito/examples/assets/image.jpg")
    img1.set_hexpand(true)
    img1.set_vexpand(true)
    img1.set_radius(26)
    item1.add(img1)
    carousel.add(item1)

    let item2 = cogito.carousel_item()
    item2.set_vexpand(true)
    item2.set_hexpand(true)
    item2.set_text(@"Lakeside")
    item2.set_halign(0)  -- left
    item2.set_valign(2)  -- bottom
    let img2 = cogito.image(@"")
    img2.set_source(@"cogito/examples/assets/image2.jpg")
    img2.set_hexpand(true)
    img2.set_vexpand(true)
    img2.set_radius(26)
    item2.add(img2)
    carousel.add(item2)

    let item3 = cogito.carousel_item()
    item3.set_vexpand(true)
    item3.set_hexpand(true)
    item3.set_text(@"Sunset Vistas")
    item3.set_halign(0)  -- left
    item3.set_valign(2)  -- bottom
    let img3 = cogito.image(@"")
    img3.set_source(@"cogito/examples/assets/image3.jpg")
    img3.set_hexpand(true)
    img3.set_vexpand(true)
    img3.set_radius(26)
    item3.add(img3)
    carousel.add(item3)

    let item4 = cogito.carousel_item()
    item4.set_vexpand(true)
    item4.set_hexpand(true)
    item4.set_text(@"Forest Trail")
    item4.set_halign(0)  -- left
    item4.set_valign(2)  -- bottom
    let img4 = cogito.image(@"")
    img4.set_source(@"cogito/examples/assets/image4.jpg")
    img4.set_hexpand(true)
    img4.set_vexpand(true)
    img4.set_radius(26)
    item4.add(img4)
    carousel.add(item4)

    let item5 = cogito.carousel_item()
    item5.set_vexpand(true)
    item5.set_hexpand(true)
    item5.set_text(@"Ocean Breeze")
    item5.set_halign(0)  -- left
    item5.set_valign(2)  -- bottom
    let img5 = cogito.image(@"")
    img5.set_source(@"cogito/examples/assets/image5.jpg")
    img5.set_hexpand(true)
    img5.set_vexpand(true)
    img5.set_radius(26)
    item5.add(img5)
    carousel.add(item5)

    vstack.add(carousel)

    let info = cogito.label(@"Width ratios: Left (4:2:1) → Center → Right (1:2:4)")
    info.set_class(@"body")
    info.set_wrap(true)
    vstack.add(info)

    let reset_btn = cogito.button(@"Reset to Start")
    reset_btn.on_click((b = cogito.Button) => {
        carousel.set_active_index(0)
        status.set_text(@"Carousel reset. Active item: 0")
    })
    vstack.add(reset_btn)

    return vstack
}

fun build_navrail_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Navigation Rail")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let status = cogito.label(@"Reference layouts: no menu, menu, FAB, and menu+FAB.")
    status.set_class(@"body")
    status.set_wrap(true)
    vstack.add(status)

    let labels = [@"Home", @"Search", @"Profile"]
    let icons = [@"sf:house", @"sf:magnifyingglass", @"sf:person"]

    let rails_row = cogito.hstack()
    rails_row.set_hexpand(true)
    rails_row.set_vexpand(true)
    rails_row.set_gap(24)

    let rail_1_col = cogito.vstack()
    rail_1_col.set_vexpand(true)
    rail_1_col.set_gap(6)
    rail_1_col.add(cogito.label(@"1. No Menu"))
    let rail_1 = cogito.nav_rail()
    rail_1.set_vexpand(true)
    rail_1.set_items(labels, icons)
    rail_1.set_toggle(false)
    rail_1.set_selected(0)
    rail_1.on_change((idx) => {
        let item = if idx == 0 {
            @"Home"
        } elif idx == 1 {
            @"Search"
        } else {
            @"Profile"
        }
        status.set_text(@"Layout 1 selected: $item")
    })
    rail_1_col.add(rail_1)
    rails_row.add(rail_1_col)

    let rail_2_col = cogito.vstack()
    rail_2_col.set_vexpand(true)
    rail_2_col.set_gap(6)
    rail_2_col.add(cogito.label(@"2. Menu"))
    let rail_2 = cogito.nav_rail()
    rail_2.set_vexpand(true)
    rail_2.set_items(labels, icons)
    rail_2.set_toggle(true)
    rail_2.set_selected(0)
    rail_2.on_change((idx) => {
        let item = if idx == 0 {
            @"Home"
        } elif idx == 1 {
            @"Search"
        } else {
            @"Profile"
        }
        status.set_text(@"Layout 2 selected: $item")
    })
    rail_2_col.add(rail_2)
    rails_row.add(rail_2_col)

    let rail_3_col = cogito.vstack()
    rail_3_col.set_vexpand(true)
    rail_3_col.set_gap(6)
    rail_3_col.add(cogito.label(@"3. FAB"))
    let rail_3 = cogito.nav_rail()
    rail_3.set_vexpand(true)
    rail_3.set_items(labels, icons)
    rail_3.set_toggle(false)
    rail_3.set_selected(0)
    let rail_3_fab = cogito.fab(@"sf:pencil")
    rail_3_fab.on_click((f = cogito.FAB) => {
        status.set_text(@"Layout 3 FAB clicked.")
    })
    rail_3.add(rail_3_fab)
    rail_3.on_change((idx) => {
        let item = if idx == 0 {
            @"Home"
        } elif idx == 1 {
            @"Search"
        } else {
            @"Profile"
        }
        status.set_text(@"Layout 3 selected: $item")
    })
    rail_3_col.add(rail_3)
    rails_row.add(rail_3_col)

    let rail_4_col = cogito.vstack()
    rail_4_col.set_vexpand(true)
    rail_4_col.set_gap(6)
    rail_4_col.add(cogito.label(@"4. Menu + FAB"))
    let rail_4 = cogito.nav_rail()
    rail_4.set_vexpand(true)
    rail_4.set_items(labels, icons)
    rail_4.set_toggle(true)
    rail_4.set_badges([1, -1, 0])
    rail_4.set_selected(0)
    let rail_4_fab = cogito.fab(@"sf:pencil")
    rail_4_fab.on_click((f = cogito.FAB) => {
        status.set_text(@"Layout 4 FAB clicked.")
    })
    rail_4.add(rail_4_fab)
    rail_4.on_change((idx) => {
        let item = if idx == 0 {
            @"Home"
        } elif idx == 1 {
            @"Search"
        } else {
            @"Profile"
        }
        status.set_text(@"Layout 4 selected: $item")
    })
    rail_4_col.add(rail_4)
    rails_row.add(rail_4_col)

    vstack.add(rails_row)
    return vstack
}

fun build_dialogs_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Dialogs & Toasts")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let status = cogito.label(@"Open a dialog or show toast notifications.")
    status.set_class(@"body")
    status.set_wrap(true)
    vstack.add(status)

    let show_btn = cogito.button(@"Show Dialog")
    vstack.add(show_btn)

    let toast_btn = cogito.button(@"Show Toast")
    vstack.add(toast_btn)

    let slot = cogito.dialog_slot()
    vstack.add(slot)

    let toast_container = cogito.toasts()
    vstack.add(toast_container)

    toast_btn.on_click((b = cogito.Button) => {
        let t = cogito.toast(@"Hello from toast! Click to update status.")
        t.on_click((tt = cogito.Toast) => {
            status.set_text(@"Toast clicked.")
        })
        toast_container.add(t)
        status.set_text(@"Toast shown.")
    })

    show_btn.on_click((b = cogito.Button) => {
        let dlg = cogito.dialog(@"Example Dialog")

        let content = cogito.vstack()
        content.set_gap(8)
        content.add(cogito.label(@"This is a dialog."))
        let close_btn = cogito.button(@"Close")
        close_btn.on_click((cb = cogito.Button) => {
            dlg.close()
            status.set_text(@"Dialog closed.")
        })
        content.add(close_btn)

        dlg.add(content)
        slot.show(dlg)
        status.set_text(@"Dialog opened.")
    })

    return vstack
}

fun build_tree_view(views = cogito.ViewSwitcher) (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Tree View")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let info = cogito.label(@"Use chevrons to collapse branches. Click leaf rows to navigate.")
    info.set_class(@"body")
    info.set_wrap(true)
    vstack.add(info)

    let tree = cogito.treeview()

    let branch_inputs = cogito.vstack()
    branch_inputs.set_class(@"inputs")
    let text_leaf = cogito.button(@"Text Fields")
    text_leaf.on_click((b = cogito.Button) => {
        views.set_active(@"textfields")
        info.set_text(@"Opened Text Fields view.")
    })
    branch_inputs.add(text_leaf)
    let picker_leaf = cogito.button(@"Pickers")
    picker_leaf.on_click((b = cogito.Button) => {
        views.set_active(@"pickers")
        info.set_text(@"Opened Pickers view.")
    })
    branch_inputs.add(picker_leaf)

    let branch_collections = cogito.vstack()
    branch_collections.set_class(@"collections")
    let list_leaf = cogito.button(@"Lists")
    list_leaf.on_click((b = cogito.Button) => {
        views.set_active(@"lists")
        info.set_text(@"Opened Lists view.")
    })
    branch_collections.add(list_leaf)
    let tree_leaf = cogito.button(@"Tree View")
    tree_leaf.on_click((b = cogito.Button) => {
        views.set_active(@"tree")
        info.set_text(@"Already on Tree View.")
    })
    branch_collections.add(tree_leaf)

    let branch_actions = cogito.vstack()
    branch_actions.set_class(@"actions")
    let dialog_leaf = cogito.button(@"Dialogs")
    dialog_leaf.on_click((b = cogito.Button) => {
        views.set_active(@"dialogs")
        info.set_text(@"Opened Dialogs view.")
    })
    branch_actions.add(dialog_leaf)
    let controls_leaf = cogito.button(@"Controls")
    controls_leaf.on_click((b = cogito.Button) => {
        views.set_active(@"controls")
        info.set_text(@"Opened Controls view.")
    })
    branch_actions.add(controls_leaf)

    tree.add(branch_inputs)
    tree.add(branch_collections)
    tree.add(branch_actions)
    tree.set_hexpand(true)
    tree.set_vexpand(true)
    vstack.add(tree)

    return vstack
}

fun build_cards_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Cards & Containers")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let info = cogito.label(@"Cards are elevated containers with rounded corners.")
    info.set_class(@"body")
    info.set_wrap(true)
    vstack.add(info)

    let card1 = cogito.card(@"Basic Card")
    card1.set_hexpand(true)
    let card1_body = cogito.label(@"This is a simple card with a title and body text.")
    card1_body.set_class(@"body")
    card1_body.set_wrap(true)
    card1.add(card1_body)
    vstack.add(card1)

    let card2 = cogito.card(@"Interactive Card")
    card2.set_hexpand(true)
    let card2_body = cogito.label(@"Cards can contain any widgets.")
    card2_body.set_class(@"body")
    card2_body.set_wrap(true)
    card2.add(card2_body)
    let card2_btn = cogito.button(@"Action")
    cogito.set_class(card2_btn, @"outlined")
    card2_btn.on_click((b = cogito.Button) => {
        info.set_text(@"Card action clicked!")
    })
    card2.add(card2_btn)
    vstack.add(card2)

    let card3 = cogito.card_untitled()
    card3.set_hexpand(true)
    let card3_body = cogito.label(@"A card without a title.")
    card3_body.set_class(@"body")
    card3_body.set_wrap(true)
    card3.add(card3_body)
    vstack.add(card3)

    return vstack
}

fun build_avatars_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Avatars & Badges")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let info = cogito.label(@"Avatars show user initials or images. Badges show counts.")
    info.set_class(@"body")
    info.set_wrap(true)
    vstack.add(info)

    let row1 = cogito.hstack()
    row1.set_gap(12)

    let av1 = cogito.avatar(@"Alice")
    row1.add(av1)

    let av2 = cogito.avatar(@"Bob")
    row1.add(av2)

    let av3 = cogito.avatar(@"Charlie")
    row1.add(av3)

    let av4 = cogito.avatar(@"sf:person")
    row1.add(av4)

    vstack.add(row1)

    let badge_label = cogito.label(@"Badges")
    badge_label.set_class(@"subtitle")
    vstack.add(badge_label)

    let row2 = cogito.hstack()
    row2.set_gap(16)

    let b1 = cogito.badge_dot()
    row2.add(b1)

    let b2 = cogito.badge(3)
    row2.add(b2)

    let b3 = cogito.badge(42)
    row2.add(b3)

    let b4 = cogito.badge(999)
    row2.add(b4)

    vstack.add(row2)

    return vstack
}

fun build_banners_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Banners & Dividers")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let info = cogito.label(@"Banners display important messages. Dividers separate content.")
    info.set_class(@"body")
    info.set_wrap(true)
    vstack.add(info)

    let banner1 = cogito.banner(@"Your connection was lost.")
    banner1.set_hexpand(true)
    banner1.set_icon(@"sf:warninexclamationmark.triangle")
    banner1.set_action(@"Retry", (b = any) => {
        info.set_text(@"Retry clicked!")
    })
    vstack.add(banner1)

    let banner2 = cogito.banner(@"A new update is available.")
    banner2.set_hexpand(true)
    banner2.set_action(@"Update", (b = any) => {
        info.set_text(@"Update clicked!")
    })
    vstack.add(banner2)

    let div_label = cogito.label(@"Dividers")
    div_label.set_class(@"subtitle")
    vstack.add(div_label)

    let item1 = cogito.label(@"Item above divider")
    item1.set_class(@"body")
    vstack.add(item1)

    let d1 = cogito.divider(@"horizontal", false)
    d1.set_hexpand(true)
    vstack.add(d1)

    let item2 = cogito.label(@"Item below divider")
    item2.set_class(@"body")
    vstack.add(item2)

    let d2 = cogito.divider(@"horizontal", true)
    d2.set_hexpand(true)
    vstack.add(d2)

    let item3 = cogito.label(@"Item below inset divider")
    item3.set_class(@"body")
    vstack.add(item3)

    return vstack
}

fun build_timepicker_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Time Picker")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let info = cogito.label(@"Select a time using the up/down buttons.")
    info.set_class(@"body")
    info.set_wrap(true)
    vstack.add(info)

    let tp = cogito.timepicker()
    tp.on_change((picker = cogito.TimePicker) => {
        let h = picker.hour()
        let m = picker.minute()
        info.set_text(@"Selected: $h:$m")
    })
    vstack.add(tp)

    return vstack
}

fun build_new_widgets_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"New Widgets")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let info = cogito.label(@"ActiveIndicator, SwitchBar, ContentList, EmptyPage, and SplitButton.")
    info.set_class(@"body")
    info.set_wrap(true)
    vstack.add(info)

    -- ActiveIndicator
    let ai_label = cogito.label(@"Active Indicator (spinner)")
    ai_label.set_class(@"body")
    vstack.add(ai_label)

    let ai = cogito.active_indicator()
    vstack.add(ai)

    -- SwitchBar
    let sb_label = cogito.label(@"SwitchBar")
    sb_label.set_class(@"body")
    vstack.add(sb_label)

    let sb = cogito.switchbar(@"Enable notifications")
    sb.set_hexpand(true)
    sb.on_change((s = any) => {
        info.set_text(@"SwitchBar toggled.")
    })
    vstack.add(sb)

    -- SplitButton
    let sp_label = cogito.label(@"SplitButton")
    sp_label.set_class(@"body")
    vstack.add(sp_label)

    let sp = cogito.split_button(@"Save")
    sp.on_click((b = any) => {
        info.set_text(@"SplitButton main action clicked.")
    })
    sp.add_menu(@"Save As...", (b = any) => {
        info.set_text(@"Save As selected.")
    })
    sp.add_menu(@"Export", (b = any) => {
        info.set_text(@"Export selected.")
    })
    vstack.add(sp)

    -- ContentList
    let cl_label = cogito.label(@"ContentList")
    cl_label.set_class(@"body")
    vstack.add(cl_label)

    let cl = cogito.content_list()
    cl.set_hexpand(true)
    cl.add(cogito.label(@"First item"))
    cl.add(cogito.label(@"Second item"))
    cl.add(cogito.label(@"Third item"))
    vstack.add(cl)

    return vstack
}

fun build_settings_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"Settings Widgets")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let info = cogito.label(@"SettingsWindow, SettingsPage, SettingsList, and SettingsRow.")
    info.set_class(@"body")
    info.set_wrap(true)
    vstack.add(info)

    let sw = cogito.settings_window(@"Preferences")
    sw.set_hexpand(true)
    sw.set_vexpand(true)

    let page1 = cogito.settings_page(@"General")
    let list1 = cogito.settings_list(@"Appearance")
    list1.set_hexpand(true)

    let row1 = cogito.settings_row(@"Dark Mode")
    row1.set_hexpand(true)
    let dark_switch = cogito.switch(@"")
    dark_switch.on_change((s = cogito.Switch) => {
        info.set_text(@"Dark mode toggled.")
    })
    row1.add(dark_switch)
    list1.add(row1)

    let row2 = cogito.settings_row(@"Font Size")
    row2.set_hexpand(true)
    let font_dd = cogito.dropdown()
    font_dd.set_items([@"Small", @"Medium", @"Large"])
    font_dd.set_selected(1)
    row2.add(font_dd)
    list1.add(row2)

    page1.add(list1)
    sw.add(page1)

    let page2 = cogito.settings_page(@"About")
    let list2 = cogito.settings_list(@"App Info")
    list2.set_hexpand(true)
    let row3 = cogito.settings_row(@"Version")
    row3.set_hexpand(true)
    row3.add(cogito.label(@"1.0.0"))
    list2.add(row3)
    page2.add(list2)
    sw.add(page2)

    vstack.add(sw)

    return vstack
}

fun build_views_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"View Widgets")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let info = cogito.label(@"ViewDual (split pane) and ViewChooser (dropdown view selector).")
    info.set_class(@"body")
    info.set_wrap(true)
    vstack.add(info)

    -- ViewDual
    let vd_label = cogito.label(@"ViewDual (drag the divider)")
    vd_label.set_class(@"body")
    vstack.add(vd_label)

    let vd = cogito.view_dual()
    vd.set_hexpand(true)
    vd.set_vexpand(true)
    vd.set_ratio(0.4)

    let left_pane = cogito.vstack()
    left_pane.set_hexpand(true)
    left_pane.set_vexpand(true)
    left_pane.set_gap(8)
    left_pane.set_padding(12, 12, 12, 12)
    left_pane.add(cogito.label(@"Left Pane"))
    left_pane.add(cogito.label(@"Resize by dragging the divider."))
    vd.add(left_pane)

    let right_pane = cogito.vstack()
    right_pane.set_hexpand(true)
    right_pane.set_vexpand(true)
    right_pane.set_gap(8)
    right_pane.set_padding(12, 12, 12, 12)
    right_pane.add(cogito.label(@"Right Pane"))
    right_pane.add(cogito.label(@"Content goes here."))
    vd.add(right_pane)

    vstack.add(vd)

    -- ViewChooser
    let vc_label = cogito.label(@"ViewChooser")
    vc_label.set_class(@"body")
    vstack.add(vc_label)

    let vc_views = cogito.view_switcher()
    vc_views.set_hexpand(true)
    vc_views.add(@"option_a", (vs = cogito.ViewSwitcher) => cogito.label(@"Option A selected"))
    vc_views.add(@"option_b", (vs = cogito.ViewSwitcher) => cogito.label(@"Option B selected"))
    vc_views.add(@"option_c", (vs = cogito.ViewSwitcher) => cogito.label(@"Option C selected"))
    vc_views.set_active(@"option_a")

    let vc = cogito.view_chooser()
    vc.set_items([@"Option A", @"Option B", @"Option C"])
    vc.bind(vc_views)
    vstack.add(vc)
    vstack.add(vc_views)

    return vstack
}

fun build_about_view() (( cogito.VStack )) {
    let vstack = cogito.vstack()
    vstack.set_vexpand(true)
    vstack.set_hexpand(true)
    vstack.set_gap(12)
    vstack.set_padding(0, 18, 0, 18)

    let subtitle = cogito.label(@"About & Welcome")
    subtitle.set_class(@"subtitle")
    vstack.add(subtitle)

    let info = cogito.label(@"AboutWindow, WelcomeScreen, and EmptyPage.")
    info.set_class(@"body")
    info.set_wrap(true)
    vstack.add(info)

    -- AboutWindow
    let aw = cogito.about_window(@"Cogito Gallery", @"1.0.0")
    aw.set_description(@"A showcase of all Cogito UI widgets.")
    aw.set_website(@"https://ergo-lang.org")
    vstack.add(aw)

    -- EmptyPage
    let ep_label = cogito.label(@"EmptyPage")
    ep_label.set_class(@"body")
    vstack.add(ep_label)

    let ep = cogito.empty_page(@"No Items Yet")
    ep.set_description(@"Add some items to get started.")
    ep.set_icon(@"sf:folder")
    ep.set_action(@"Add Item", (b = any) => {
        info.set_text(@"EmptyPage action clicked!")
    })
    ep.set_hexpand(true)
    vstack.add(ep)

    return vstack
}

fun build_ui(win = cogito.Window) (( -- )) {
    let bar = cogito.appbar(@"Sidebar", @"")
    cogito.set_class(bar, @"sidebar")
    let bar2 = cogito.appbar(@"Gallery", @"")

    let hstack = cogito.hstack()
    hstack.set_vexpand(true)
    hstack.set_hexpand(true)

    let views = cogito.view_switcher()
    views.set_hexpand(true)
    views.set_vexpand(true)

    -- Register lazy view builders - views are built on-demand when first selected
    views.add(@"buttons", (vs = cogito.ViewSwitcher) => build_buttons_view())
    views.add(@"textfields", (vs = cogito.ViewSwitcher) => build_textfields_view())
    views.add(@"checkboxes", (vs = cogito.ViewSwitcher) => build_checkboxes_view())
    views.add(@"sliders", (vs = cogito.ViewSwitcher) => build_sliders_view())
    views.add(@"pickers", (vs = cogito.ViewSwitcher) => build_pickers_view())
    views.add(@"lists", (vs = cogito.ViewSwitcher) => build_lists_view())
    views.add(@"controls", (vs = cogito.ViewSwitcher) => build_controls_view())
    views.add(@"tabs", (vs = cogito.ViewSwitcher) => build_tabs_view())
    views.add(@"chips", (vs = cogito.ViewSwitcher) => build_chips_view())
    views.add(@"carousel", (vs = cogito.ViewSwitcher) => build_carousel_view())
    views.add(@"navrail", (vs = cogito.ViewSwitcher) => build_navrail_view())
    views.add(@"dialogs", (vs = cogito.ViewSwitcher) => build_dialogs_view())
    views.add(@"tree", (vs = cogito.ViewSwitcher) => build_tree_view(vs))
    views.add(@"cards", (vs = cogito.ViewSwitcher) => build_cards_view())
    views.add(@"avatars", (vs = cogito.ViewSwitcher) => build_avatars_view())
    views.add(@"banners", (vs = cogito.ViewSwitcher) => build_banners_view())
    views.add(@"timepicker", (vs = cogito.ViewSwitcher) => build_timepicker_view())
    views.add(@"newwidgets", (vs = cogito.ViewSwitcher) => build_new_widgets_view())
    views.add(@"settings", (vs = cogito.ViewSwitcher) => build_settings_view())
    views.add(@"viewwidgets", (vs = cogito.ViewSwitcher) => build_views_view())
    views.add(@"about", (vs = cogito.ViewSwitcher) => build_about_view())

    -- Set initial active view
    views.set_active(@"buttons")

    -- Sidebar
    let sidebar = cogito.vstack()
    cogito.set_class(sidebar, @"sidebar")
    sidebar.set_vexpand(true)
    sidebar.add(bar)

    let sidebar_list = cogito.list()
    sidebar_list.set_vexpand(true)
    sidebar_list.add(cogito.label(@"Buttons"))
    sidebar_list.add(cogito.label(@"Text Fields"))
    sidebar_list.add(cogito.label(@"Checkboxes"))
    sidebar_list.add(cogito.label(@"Sliders"))
    sidebar_list.add(cogito.label(@"Pickers"))
    sidebar_list.add(cogito.label(@"Lists"))
    sidebar_list.add(cogito.label(@"Controls"))
    sidebar_list.add(cogito.label(@"Tabs"))
    sidebar_list.add(cogito.label(@"Chips & FAB"))
    sidebar_list.add(cogito.label(@"Carousel"))
    sidebar_list.add(cogito.label(@"Nav Rail"))
    sidebar_list.add(cogito.label(@"Dialogs"))
    sidebar_list.add(cogito.label(@"Tree View"))
    sidebar_list.add(cogito.label(@"Cards"))
    sidebar_list.add(cogito.label(@"Avatars & Badges"))
    sidebar_list.add(cogito.label(@"Banners"))
    sidebar_list.add(cogito.label(@"Time Picker"))
    sidebar_list.add(cogito.label(@"New Widgets"))
    sidebar_list.add(cogito.label(@"Settings"))
    sidebar_list.add(cogito.label(@"View Widgets"))
    sidebar_list.add(cogito.label(@"About & Welcome"))
    sidebar_list.on_select((idx) => {
        let target = if idx == 0 {
            @"buttons"
        } elif idx == 1 {
            @"textfields"
        } elif idx == 2 {
            @"checkboxes"
        } elif idx == 3 {
            @"sliders"
        } elif idx == 4 {
            @"pickers"
        } elif idx == 5 {
            @"lists"
        } elif idx == 6 {
            @"controls"
        } elif idx == 7 {
            @"tabs"
        } elif idx == 8 {
            @"chips"
        } elif idx == 9 {
            @"carousel"
        } elif idx == 10 {
            @"navrail"
        } elif idx == 11 {
            @"dialogs"
        } elif idx == 12 {
            @"tree"
        } elif idx == 13 {
            @"cards"
        } elif idx == 14 {
            @"avatars"
        } elif idx == 15 {
            @"banners"
        } elif idx == 16 {
            @"timepicker"
        } elif idx == 17 {
            @"newwidgets"
        } elif idx == 18 {
            @"settings"
        } elif idx == 19 {
            @"viewwidgets"
        } elif idx == 20 {
            @"about"
        } else {
            @"buttons"
        }
        views.set_active(target)
    })

    let scroller = cogito.scroller ()
    cogito.set_class(scroller, @"sidebar")
    scroller.set_axes(false, true)
    scroller.add(sidebar_list)
    scroller.set_vexpand(true)
    scroller.set_hexpand(false)

    sidebar.add(scroller)

    hstack.add(sidebar)

    -- Main content area
    let content = cogito.vstack()
    content.add(bar2)
    content.set_hexpand(true)
    content.set_vexpand(true)

    let scroller2 = cogito.scroller()
    scroller2.set_axes(false, true)
    scroller2.add(views)
    scroller2.set_vexpand(true)
    scroller2.set_hexpand(true)
    content.add(scroller2)
    hstack.add(content)

    win.add(hstack)
}

entry () (( -- )) {
    let app = cogito.app()
    let main_win = cogito.window_size(@"Cogito Gallery", 800, 600)
    main_win.build(build_ui)
    app.set_appid(@"ergo.cogito.Gallery")
    app.set_accent_color(@"#75dec2", false)
    main_win.set_a11y_label(@"Cogito Gallery")
    main_win.set_resizable(true)
    app.run(main_win)
}
